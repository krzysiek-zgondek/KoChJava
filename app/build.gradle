import java.security.spec.InvalidKeySpecException

apply plugin: 'com.android.application'
apply plugin: 'io.objectbox'

buildscript {
    ext.objectboxVersion = '1.5.0'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "io.objectbox:objectbox-gradle-plugin:$objectboxVersion"
    }
}

android {
    compileSdkVersion 27

    signingConfigs{
        release{
            def propertiesFile = rootProject.file("/keys/keystore.properties")

            if (propertiesFile.exists()) {
                def properties = new Properties()
                properties.load(new FileInputStream(propertiesFile))
                storeFile = file(properties["storeFile"])
                storePassword = properties["storePassword"]
                keyAlias = properties["releaseKeyAlias"]
                keyPassword = properties["releaseKeyPassword"]
            } else throw new InvalidKeySpecException("you need a proper keystore.properties file")
        }
    }

    defaultConfig {
        applicationId "com.koch.java.kochjava"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true
        aaptOptions {
            additionalParameters "--no-version-vectors"
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "4g"
        jumboMode true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    implementation "android.arch.lifecycle:extensions:1.1.1"
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    implementation("com.jakewharton:butterknife:8.8.1") {
        exclude group: "com.android.support", module: "support-annotations"
    }
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    implementation 'com.google.dagger:dagger-android:2.11'
    implementation 'com.google.dagger:dagger-android-support:2.11'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.11'

    implementation 'org.greenrobot:eventbus:3.1.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:design:27.1.1'
}

android.applicationVariants.all { variant ->
    if (variant.buildType.name == "debug") {
        return
    }
    variant.outputs.all { output ->
        outputFileName = "TruthTeller_${variant.name}_${android.defaultConfig.versionName}_${System.getenv("BUILD_NUMBER") as Integer ?: 0}.apk"
    }
}
